#!/bin/zsh
# Focus the application currently playing audio

BT="/Users/kevin/.local/bin/bt"
RG="/opt/homebrew/bin/rg"
AEROSPACE="/opt/homebrew/bin/aerospace"
JQ="/opt/homebrew/bin/jq"
STATE_FILE="$HOME/.local/state/focus-now-playing"

# Get power assertions
assertions=$(LC_ALL=C pmset -g assertions)

# Get all PIDs with coreaudiod audio assertions (youngest first)
audio_pids=$(echo "$assertions" | awk '
  /coreaudiod.*audio/ && match($0, /[0-9]+:[0-9]+:[0-9]+/) {
    time = substr($0, RSTART, RLENGTH)
    getline; if (/Created for PID:/ && match($0, /[0-9]+/)) print time, substr($0, RSTART, RLENGTH)
  }' 2>/dev/null | sort -t: -k1,1n -k2,2n -k3,3n | cut -d' ' -f2)

# Find first PID that's actually playing (skip browsers without audible tabs)
pid=""
for candidate in ${(f)audio_pids}; do
  candidate_path=$(ps -p "$candidate" -o comm= 2>/dev/null)
  # Skip XPC services and helper processes - they aren't launchable apps
  [[ "$candidate_path" == *"/XPCServices/"* || "$candidate_path" == *".xpc/"* || "$candidate_path" == *"/Helpers/"* ]] && continue
  # Check if it's Zen Browser - only use if it has audible tabs
  if [[ "$candidate_path" == *"Zen Browser"* || "$candidate_path" == */zen ]]; then
    if $BT query +audible 2>/dev/null | $RG -q .; then
      pid="$candidate"
      break
    fi
  else
    pid="$candidate"
    break
  fi
done
if [[ -n "$pid" ]]; then
  app_path=$(ps -p "$pid" -o comm= 2>/dev/null)
  if [[ -n "$app_path" ]]; then
    if [[ "$app_path" == /Applications/*.app/* ]]; then
      # Extract main app name from /Applications/Foo.app/... paths
      app_name="${app_path#/Applications/}"
      app_name="${app_name%%/*}"
      app_name="${app_name%.app}"
    elif [[ "$app_path" == *".app/"* ]]; then
      app_name="${${app_path%.app/*}##*/}"
    else
      app_name="${app_path##*/}"
    fi
  fi
fi

# If no app playing, try fallback to last playing app (excluding Zen)
if [[ -z "$app_name" ]]; then
  if [[ -f "$STATE_FILE" ]]; then
    last_app=$(<"$STATE_FILE")
    if [[ -n "$last_app" ]]; then
      open -a "$last_app" 2>/dev/null
    fi
  fi
  exit 0
fi

# If it's Zen Browser, focus the specific audible tab and window via aerospace
if [[ "${app_name:l}" == "zen" || "${app_name:l}" == "zen browser" ]]; then
  read -r tab_line <<< "$($BT query +audible 2>/dev/null)"
  if [[ -n "$tab_line" ]]; then
    IFS=$'\t' read -r tab_id tab_title _ <<< "$tab_line"
    $BT activate "$tab_id" 2>/dev/null

    # Use first 20 chars of title for fuzzy match
    window_id=$($AEROSPACE list-windows --all --json | $JQ -r \
      --arg title "${tab_title:0:20}" \
      'first(.[] | select(."app-name" == "Zen" and (.title | contains($title)))) | ."window-id"')

    if [[ -n "$window_id" && "$window_id" != "null" ]]; then
      $AEROSPACE focus --window-id "$window_id" 2>/dev/null
    else
      open -a "Zen Browser" 2>/dev/null
    fi
  else
    open -a "Zen Browser" 2>/dev/null
  fi
else
  # Activate other apps normally and save as last playing
  echo "$app_name" > "$STATE_FILE"
  open -a "$app_name" 2>/dev/null
fi

#!/bin/zsh
# Focus the application currently playing audio

BT="/Users/kevin/.local/bin/bt"
RG="/opt/homebrew/bin/rg"
YABAI="/opt/homebrew/bin/yabai"
JQ="/opt/homebrew/bin/jq"

# Get power assertions
assertions=$(LC_ALL=C pmset -g assertions)

# Get all PIDs with coreaudiod audio assertions (youngest first)
audio_pids=$(echo "$assertions" | awk '
  /coreaudiod.*audio/ && match($0, /[0-9]+:[0-9]+:[0-9]+/) {
    time = substr($0, RSTART, RLENGTH)
    getline; if (/Created for PID:/ && match($0, /[0-9]+/)) print time, substr($0, RSTART, RLENGTH)
  }' | sort -t: -k1,1n -k2,2n -k3,3n | cut -d' ' -f2)

# Find first PID that's actually playing (skip browsers without audible tabs)
pid=""
for candidate in ${(f)audio_pids}; do
  candidate_path=$(ps -p "$candidate" -o comm= 2>/dev/null)
  # Check if it's Zen Browser - only use if it has audible tabs
  if [[ "$candidate_path" == *"Zen Browser"* || "$candidate_path" == */zen ]]; then
    if $BT query +audible 2>/dev/null | $RG -q .; then
      pid="$candidate"
      break
    fi
  else
    pid="$candidate"
    break
  fi
done
if [[ -n "$pid" ]]; then
  app_path=$(ps -p "$pid" -o comm= 2>/dev/null)
  if [[ -n "$app_path" ]]; then
    if [[ "$app_path" == /Applications/*.app/* ]]; then
      # Extract main app name from /Applications/Foo.app/... paths
      app_name="${app_path#/Applications/}"
      app_name="${app_name%%/*}"
      app_name="${app_name%.app}"
    elif [[ "$app_path" == *".app/"* ]]; then
      app_name="${${app_path%.app/*}##*/}"
    else
      app_name="${app_path##*/}"
    fi
  fi
fi

[[ -z "$app_name" ]] && exit 0

# If it's Zen Browser, focus the specific audible tab and window via yabai
if [[ "${app_name:l}" == "zen" || "${app_name:l}" == "zen browser" ]]; then
  tab_line=$($BT query +audible 2>/dev/null | head -1)
  if [[ -n "$tab_line" ]]; then
    tab_id=$(echo "$tab_line" | awk '{print $1}')
    tab_title=$(echo "$tab_line" | cut -f2)
    $BT activate "$tab_id" 2>/dev/null

    # Use first 20 chars of title for fuzzy match
    title_prefix="${tab_title:0:20}"
    window_info=$($YABAI -m query --windows | $JQ -r \
      --arg title "$title_prefix" \
      '.[] | select(.app == "Zen" and (.title | contains($title))) | "\(.id) \(.["has-focus"])"' | head -1)
    window_id=$(echo "$window_info" | awk '{print $1}')
    has_focus=$(echo "$window_info" | awk '{print $2}')

    if [[ -n "$window_id" && "$has_focus" != "true" ]]; then
      $YABAI -m window "$window_id" --focus
      $YABAI -m window "$window_id" --raise
    elif [[ -z "$window_id" ]]; then
      open -a "Zen Browser" 2>/dev/null
    fi
  else
    open -a "Zen Browser" 2>/dev/null
  fi
else
  # Activate other apps normally
  open -a "$app_name" 2>/dev/null
fi

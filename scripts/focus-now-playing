#!/bin/zsh
# Focus the application currently playing audio

BT="/Users/kevin/.local/bin/bt"
RG="/opt/homebrew/bin/rg"

# Get power assertions
assertions=$(LC_ALL=C pmset -g assertions)

# Get all PIDs with coreaudiod audio assertions
audio_pids=$(echo "$assertions" | awk '
  /coreaudiod.*audio/ && match($0, /[0-9]+:[0-9]+:[0-9]+/) {
    time = substr($0, RSTART, RLENGTH)
    getline; if (/Created for PID:/ && match($0, /[0-9]+/)) print time, substr($0, RSTART, RLENGTH)
  }' | sort -t: -k1,1n -k2,2n -k3,3n | cut -d' ' -f2)

# Method 1: Prefer apps with direct playback assertions (e.g., Spotify's "Video Wake Lock")
for pid in ${(f)audio_pids}; do
  app_path=$(ps -p "$pid" -o comm= 2>/dev/null)
  [[ -z "$app_path" ]] && continue
  if [[ "$app_path" == *".app/"* ]]; then
    check_name="${${app_path%.app/*}##*/}"
  else
    check_name="${app_path##*/}"
  fi
  # Check if this app has its own playback assertion (not just coreaudiod)
  if echo "$assertions" | $RG -q "pid [0-9]+\($check_name\):.*(NoDisplaySleepAssertion|NoIdleSleepAssertion)"; then
    app_name="$check_name"
    break
  fi
done

# Method 2: Fall back to youngest coreaudiod assertion
if [[ -z "$app_name" ]]; then
  pid=$(echo "$audio_pids" | head -n1)
  if [[ -n "$pid" ]]; then
    app_path=$(ps -p "$pid" -o comm= 2>/dev/null)
    if [[ "$app_path" == *".app/"* ]]; then
      app_name="${${app_path%.app/*}##*/}"
    else
      app_name="${app_path##*/}"
    fi
  fi
fi

[[ -z "$app_name" ]] && exit 0

# If it's Zen Browser, focus the specific audible tab via brotab first
if [[ "${app_name:l}" == "zen" || "${app_name:l}" == "zen browser" ]]; then
  tab_id=$($BT query +audible 2>/dev/null | head -1 | awk '{print $1}')
  [[ -n "$tab_id" ]] && $BT activate "$tab_id" 2>/dev/null
  app_name="Zen Browser"
fi

# Activate the app
open -a "$app_name" 2>/dev/null

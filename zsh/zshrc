# Enable Powerlevel10k instant prompt (should stay at the top of ~/.zshrc)
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Only run the rest for interactive shells.
[[ -o interactive ]] || return 0

# Some plugins can call `compdef` before `compinit` has run. If `_comps` isn't an
# associative array yet, those calls can fail with:
#   _comps: assignment to invalid subscript range
typeset -g -A _comps _services _patcomps _postpatcomps

# https://github.com/romkatv/powerlevel10k
if [[ -r /opt/homebrew/share/powerlevel10k/powerlevel10k.zsh-theme ]]; then
  source /opt/homebrew/share/powerlevel10k/powerlevel10k.zsh-theme
elif [[ -r /usr/local/share/powerlevel10k/powerlevel10k.zsh-theme ]]; then
  source /usr/local/share/powerlevel10k/powerlevel10k.zsh-theme
fi

# https://github.com/zsh-users/zsh-completions
for _zsh_fpath_dir in \
  /opt/homebrew/share/zsh/site-functions \
  /opt/homebrew/share/zsh-completions \
  /usr/local/share/zsh/site-functions \
  /usr/local/share/zsh-completions; do
  [[ -d "$_zsh_fpath_dir" ]] && fpath=("$_zsh_fpath_dir" $fpath)
done
unset _zsh_fpath_dir
autoload -Uz compinit

# Speed up completion initialization:
# - Use an XDG cache location for compdump/cache
# - Skip compaudit on every startup (-C). If you want the security check back,
#   remove `-C` (startup may be slower).
_zsh_cache_dir="${XDG_CACHE_HOME:-$HOME/.cache}/zsh"
command mkdir -p "$_zsh_cache_dir" 2>/dev/null
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$_zsh_cache_dir/zcompcache"
_zsh_compdump="$_zsh_cache_dir/zcompdump-${ZSH_VERSION}"
if ! compinit -C -d "$_zsh_compdump" 2>/dev/null; then
  command rm -f "$_zsh_compdump" "${_zsh_compdump}.zwc" 2>/dev/null
  compinit -i -d "$_zsh_compdump"
fi
unset _zsh_compdump
unset _zsh_cache_dir

export DOTFILES_HOME="$HOME/.dotfiles"

# https://github.com/zdharma-continuum/fast-syntax-highlighting
[[ -r "$HOME/.zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh" ]] && source "$HOME/.zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"

# https://github.com/ajeetdsouza/zoxide
(( $+commands[zoxide] )) && eval "$(zoxide init zsh)"

# https://github.com/BurntSushi/ripgrep/blob/master/GUIDE.md#configuration-file
export RIPGREP_CONFIG_PATH="$DOTFILES_HOME/ripgrep/ripgreprc"

# https://github.com/MenkeTechnologies/zsh-expand
[[ -r "$HOME/.zsh/plugins/zsh-expand/zsh-expand.plugin.zsh" ]] && source "$HOME/.zsh/plugins/zsh-expand/zsh-expand.plugin.zsh"
export ZPWR_CORRECT=false

# https://github.com/junegunn/fzf
export FZF_DEFAULT_OPTS="
--height=50%
--layout=reverse
--bind 'tab:accept'"

# https://github.com/Aloxaf/fzf-tab
[[ -r "$HOME/.zsh/plugins/fzf-tab/fzf-tab.plugin.zsh" ]] && source "$HOME/.zsh/plugins/fzf-tab/fzf-tab.plugin.zsh"
zstyle ':fzf-tab:*' fzf-bindings 'tab:accept'

# skim shell integration (enables **<TAB> recursive fuzzy completion)
[[ -r /opt/homebrew/opt/sk/share/zsh/site-functions/key-bindings.zsh ]] && source /opt/homebrew/opt/sk/share/zsh/site-functions/key-bindings.zsh

# https://zsh.sourceforge.io/Guide/zshguide04.html
WORDCHARS=${WORDCHARS/\/}

# https://github.com/ohmyzsh/ohmyzsh/blob/master/plugins/git/git.plugin.zsh
[[ -r "$HOME/.zsh/plugins/ohmyzsh/plugins/git/git.plugin.zsh" ]] && source "$HOME/.zsh/plugins/ohmyzsh/plugins/git/git.plugin.zsh"

# https://graphite.dev
[[ -r "$HOME/.zsh/plugins/gt-completions/completions.zsh" ]] && source "$HOME/.zsh/plugins/gt-completions/completions.zsh"

# https://dystroy.org/broot/
if [[ -r "$HOME/.config/broot/launcher/zsh/br" ]]; then
  source "$HOME/.config/broot/launcher/zsh/br"
elif [[ -r "$HOME/.config/broot/launcher/bash/br" ]]; then
  source "$HOME/.config/broot/launcher/bash/br"
fi

source "$DOTFILES_HOME/zsh/editor.zsh"
source "$DOTFILES_HOME/zsh/completion.zsh"
source "$DOTFILES_HOME/zsh/keymaps.zsh"
source "$DOTFILES_HOME/zsh/history/history.zsh"

# Source all alias files
for alias_file in "$DOTFILES_HOME/zsh/aliases"/*.zsh(N); do
  source "$alias_file"
done

# Source all function files
for function_file in "$DOTFILES_HOME/zsh/functions"/**/*.zsh(N); do
  source "$function_file"
done

[[ -r "$HOME/.zshrc.local" ]] && source "$HOME/.zshrc.local"

# https://mise.jdx.dev/
# Full activation with hooks for interactive shells (auto-switches versions on cd)
(( $+commands[mise] )) && eval "$(mise activate zsh)"

# bun (after mise so bun globals take precedence)
export PATH="$HOME/.bun/bin:$PATH"

# bun completions
[ -s "/Users/kevin/.bun/_bun" ] && source "/Users/kevin/.bun/_bun"

export ANDROID_HOME="$HOME/Library/Android/sdk"
[[ -d "$ANDROID_HOME/emulator" ]] && path+=("$ANDROID_HOME/emulator")
[[ -d "$ANDROID_HOME/platform-tools" ]] && path+=("$ANDROID_HOME/platform-tools")
typeset -U path
export PATH

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh
if (( $+functions[p10k] )) && [[ -r "$HOME/.p10k.zsh" ]]; then
  source "$HOME/.p10k.zsh"
fi

# https://atuin.sh/
# Disable up arrow binding, use Ctrl+P instead
eval "$(atuin init zsh --disable-up-arrow)"
bindkey '^p' _atuin_search_widget

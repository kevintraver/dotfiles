#!/bin/zsh
# Focus or open a URL in Zen Browser

if [[ -z "$1" ]]; then
  echo "Usage: zen-focus-tab <url>" >&2
  exit 1
fi

url="$1"
BT="/Users/kevin/.local/bin/bt"
YABAI="/opt/homebrew/bin/yabai"
JQ="/opt/homebrew/bin/jq"

# Convert to Chrome match pattern
[[ "$url" =~ ^https?:// ]] && base="${url%/}" || base="*://${url%/}"

# Try wildcard first, then exact for URLs with paths
# (bare *://host hangs, but *://host/path is safe)
tab_line=$($BT query -url "$base/*" 2>/dev/null | head -1)
if [[ -z "$tab_line" && "${base#*://}" == */* ]]; then
  tab_line=$($BT query -url "$base" 2>/dev/null | head -1)
fi

if [[ -n "$tab_line" ]]; then
  IFS=$'\t' read -r tab_id tab_title _ <<< "$tab_line"
  $BT activate "$tab_id"

  # Use first 20 chars of title for fuzzy match (avoids quote encoding issues)
  window_info=$($YABAI -m query --windows | $JQ -r \
    --arg title "${tab_title:0:20}" \
    'first(.[] | select(.app == "Zen" and (.title | contains($title)))) | "\(.id) \(.["has-focus"])"')
  read -r window_id has_focus <<< "$window_info"

  if [[ -n "$window_id" && "$has_focus" != "true" ]]; then
    $YABAI -m window "$window_id" --focus
  fi
else
  # Add https:// if not present
  [[ ! "$url" =~ ^https?:// ]] && url="https://$url"
  open -a "Zen Browser" "$url"
fi
